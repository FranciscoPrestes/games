buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.eriwen:gradle-js-plugin:0.3'
		classpath files(fileTree(dir: "$projectDir/plugins" as File, includes: ['*.jar']))
		classpath 'com.yahoo.platform.yui:yuicompressor:2.4.6'
	}
}

apply plugin: 'eclipse'
apply plugin: 'jetty'
apply plugin: 'maven'
apply plugin: 'scala'
apply plugin: 'js'
apply plugin: 'css'

sourceCompatibility = 1.5
targetCompatibility = 1.5

version = '0.1-SNAPSHOT'
group = 'net.vidageek'

repositories {
	mavenCentral()
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
	provided
}

sourceSets {
	main { compileClasspath += configurations.provided }
	test { runtimeClasspath += configurations.provided }
}

eclipse.classpath.plusConfigurations += configurations.provided

dependencies {
	compile 'org.prevayler:prevayler-factory:2.5'
	compile 'com.thoughtworks.xstream:xstream:1.4.2'
	compile 'log4j:log4j:1.2.16'
	compile 'org.apache.velocity:velocity:1.7'
	compile 'javax.servlet:jstl:1.2'
	compile 'opensymphony:sitemesh:2.4.2'
	compile 'com.google.guava:guava:r09'
	compile 'org.scribe:scribe:1.3.0'
	compile ('br.com.caelum:vraptor:3.4.0'){
		exclude group: 'org.springframework'
	}
	compile 'com.google.inject:guice:3.0-rc2'
	compile 'com.google.inject.extensions:guice-multibindings:3.0-rc2'
	compile files(fileTree(dir: "$projectDir/lib" as File, includes: ['*.jar']))
	compile 'org.scala-lang:scala-library:2.9.1-1'

	provided 'javax.servlet:servlet-api:2.5'
	
	testCompile 'org.mockito:mockito-core:1.9.0'
	testCompile 'junit:junit:4.10'

	scalaTools 'org.scala-lang:scala-compiler:2.9.1-1'
	scalaTools 'org.scala-lang:scala-library:2.9.1-1'
}

[jettyRunWar, jettyRun]*.contextPath = '/'

js {
	inputs.files fileTree(dir: "${webAppDirName}/js", include: "**/*.js")
	outputs.file file("${webAppDirName}/js/games-packaged.js.gz")
	warningLevel = 'QUIET'
}

css {
    inputs.files fileTree(dir: "${webAppDirName}/css", include: "**/*.css")
    outputs.file file("${webAppDirName}/css/games-packaged.css.gz")
}

war.dependsOn 'js'
war.dependsOn 'css'